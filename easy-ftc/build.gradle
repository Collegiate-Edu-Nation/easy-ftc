// SPDX-FileCopyrightText: 2024 Collegiate Edu-Nation
// SPDX-License-Identifier: GPL-3.0-or-later

plugins {
    id 'com.android.library'
    id 'com.diffplug.spotless' version '7.0.0.BETA4'
}

import org.apache.tools.ant.taskdefs.condition.Os

// vars
int tgt = 30
String dir = '..'
String bld = 'build'
String ver = 'verification'
String api = 'apidoc'
String blk = 'myBlocks'
String store = '/nix/store'
String ftcBase = 'org.firstinspires.ftc:'
String ftcV = ':10.1.1'
String hardware = ftcBase + 'Hardware' + ftcV
String core = ftcBase + 'RobotCore' + ftcV
String jre17 = System.getenv('JRE17_PATH')
String puml = System.getenv('PUML_PATH')
jre17 = (jre17 != null) ? jre17 : ''
puml = (puml != null) ? puml : ''

android {
    namespace 'org.edu_nation.easy_ftc'

    compileSdk tgt

    defaultConfig {
        minSdk 24
        targetSdk tgt
        versionCode = 1
        versionName = '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            enableUnitTestCoverage true
        }
    }

    // ignore "debug" variant
    // variantFilter has been deprecated, but 'beforeVariants' breaks the build
    variantFilter { variant ->
        if (variant.name == ("debug")) {
            variant.setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // ignores deprecation of targeting 1_8
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xlint:-options'
    }

    // set lint baseline
    lint {
        baseline = file('lint-baseline.xml')
    }
}

// prepend 'npx' on unix if nix isn't used, 'npx.cmd' on windows
static List<String> finalizeRunCmd(List<String> runCmd, String jre17, String store) {
    String npxCmd = (Os.isFamily(Os.FAMILY_WINDOWS)) ? 'npx.cmd' : 'npx'
    if (!jre17.contains(store)) {
        runCmd.add(0, npxCmd)
    }
    return runCmd
}

// formats all java files
spotless {
    java {
        googleJavaFormat().aosp() // 4 lines instead of 2 (default)
        target '**/*.java'        // fix for android project discovery
    }
}

// formats all nix files
tasks.register('formatNix', Exec) {
    group = ver
    description = 'Formats all nix files'

    workingDir dir
    commandLine 'nixfmt', 'flake.nix', 'lib/npm-groovy-lint/default.nix'
}

// formats all groovy files
tasks.register('formatGroovy', Exec) {
    group = ver
    description = 'Formats all groovy files'

    List<String> runCmd = ['npm-groovy-lint', '--format', 'build.gradle', 'settings.gradle',
                           'easy-ftc/build.gradle', '--javaexecutable', jre17]
    runCmd = finalizeRunCmd(runCmd, jre17, store)

    workingDir dir
    commandLine runCmd
}

// formats all yml, md files
tasks.register('formatMisc', Exec) {
    group = ver
    description = 'Formats all yml, md files'

    List<String> runCmd = ['prettier', '--write', '**/*.yml', '**/*.md']
    runCmd = finalizeRunCmd(runCmd, jre17, store)

    workingDir dir
    commandLine runCmd
}

// formats all files
tasks.register('format') {
    dependsOn spotlessApply, formatNix, formatGroovy, formatMisc
    group = ver
    description = 'Formats all files'
}

// create task for compressing myBlocks to a zip archive
tasks.register(blk, Zip) {
    group = bld
    description = 'Compress myBlocks to a zip archive for release'

    archiveBaseName = blk
    destinationDirectory = file('build/outputs/zip')
    from(files('./src/myBlocks/java/org/firstinspires/ftc/teamcode'))
}

// create alias for jacoco report
tasks.register('coverage', Exec) {
    group = 'reporting'
    description = 'Generates Jacoco coverage report as HTML in build/reports/coverage'

    workingDir dir
    commandLine './gradlew', 'createReleaseUnitTestCoverageReport'
}

// create task for generating uml
tasks.register('uml', Exec) {
    group = bld
    description = 'Generate UML in docs/uml from uml.puml'

    workingDir dir
    commandLine puml, './docs/uml/src/*.puml', '-o', '../out'
    ignoreExitValue true
}

// create tasks for docs
android.libraryVariants.configureEach { variant ->
    if (variant.name == 'release') {
        // generate javadoc
        tasks.register(api, Javadoc) {
            dependsOn uml
            group = bld
            description = 'Generate the Javadoc for the public API'

            source = variant.javaCompileProvider.get().source
            classpath = files(((Object) android.bootClasspath.join(File.pathSeparator)))
            classpath += variant.javaCompileProvider.get().classpath
            destinationDir = file('../docs/javadoc')
            failOnError false
            doNotTrackState('AS workaround')

            options {
                setMemberLevel JavadocMemberLevel.PUBLIC
                setStylesheetFile file('../docs/stylesheet.css')
                addBooleanOption('Xdoclint:none', true)
            }
        }

        // generate docs
        tasks.register('docs', Exec) {
            dependsOn tasks.named(api)
            group = bld
            description = 'Generate Documentation in docs/ using MkDocs, PlantUML, and Javadoc'

            workingDir dir
            commandLine 'mkdocs', bld
        }
    }
}

dependencies {
    // src
    implementation core
    implementation hardware
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // test
    testImplementation core
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.apache.commons:commons-lang3:3.17.0'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
}
